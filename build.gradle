/*
 * Copyright (c) 2022.  SRG SSR. All rights reserved.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '7.2.1' apply false
    id 'com.android.library' version '7.2.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.10' apply false
    // https://github.com/detekt/detekt
    id 'io.gitlab.arturbosch.detekt' version '1.21.0' apply false
}

subprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"

    // Official site : https://detekt.dev/docs/gettingstarted/gradle
    // Tutorial : https://medium.com/@nagendran.p/integrating-detekt-in-the-android-studio-442128e971f8
    detekt {
        // preconfigure defaults
        buildUponDefaultConfig = true
        allRules = true
        // activate all available (even unstable) rules.
        autoCorrect = true
        // Builds the AST in parallel. Rules are always executed in parallel.
        // Can lead to speedups in larger projects. `false` by default.
        parallel = false
        reports {
            txt.enabled = false
            html.enabled = true
            xml.enabled = true
        }
    }
    dependencies {
        detekt "io.gitlab.arturbosch.detekt:detekt-cli:1.21.0"
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.21.0"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

/*
 * https://detekt.dev/docs/gettingstarted/git-pre-commit-hook
 * https://medium.com/@alistair.cerio/android-ktlint-and-pre-commit-git-hook-5dd606e230a9
 */
task installGitHook(type: Copy) {
    from new File(rootProject.rootDir, 'git_hooks/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0777
}

tasks.getByPath(':pillarbox-demo:preBuild').dependsOn installGitHook
